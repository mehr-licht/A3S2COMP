
options {
  MULTI =true;
  LOOKAHEAD=1;
  VISITOR=true;
  STATIC =true;
 // NODE_SCOPE_HOOK = true;
  ERROR_REPORTING = true;
 // JJTREE_OUTPUT_DIRECTORY = "AST_files";
}

PARSER_BEGIN(Jmm)

//package AST_files;
//import AST_files.SimpleNode;


//Class
public class Jmm {
    public static String erro = "";

    public Jmm(){

    }

    //TODO if necessary
    private static void jjtreeOpenNodeScope(Node n)	{
    	 // ((SimpleNode)n).setLineNumber(getToken(1).beginLine);
    }

    //TODO if necessary
    private static void jjtreeCloseNodeScope(Node n){
      //closeNodeHook();
    }


//  /*
//   * Returns true if the next token is not in the FOLLOW list of "expansion".
//   * It is used to decide when the end of an "expansion" has been reached.
//   */
//  static private boolean notTailOfExpansionUnit() {
//    Token t;
//    t = getToken(1);
//    if (t.kind == BIT_OR || t.kind == COMMA || t.kind == RPAREN || t.kind == RBRACE || t.kind == RBRACKET) return false;
//    return true;
//  }

}

PARSER_END(Jmm)

/* WHITE SPACE */

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
}

/* COMMENTS */

MORE :
{
  "//" : IN_SINGLE_LINE_COMMENT
|
  <"/**" ~["/"]> { input_stream.backup(1); } : IN_FORMAL_COMMENT
|
  "/*" : IN_MULTI_LINE_COMMENT
}

<IN_SINGLE_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <SINGLE_LINE_COMMENT: "\n" | "\r" | "\r\n" > : DEFAULT
}

<IN_FORMAL_COMMENT>
SPECIAL_TOKEN :
{
  <FORMAL_COMMENT: "*/" > : DEFAULT
}

<IN_MULTI_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <MULTI_LINE_COMMENT: "*/" > : DEFAULT
}

<IN_SINGLE_LINE_COMMENT,IN_FORMAL_COMMENT,IN_MULTI_LINE_COMMENT>
MORE :
{
  < ~[] >
}

/* JAVA RESERVED WORDS AND LITERALS */

TOKEN :
{
 < BOOLEAN: "boolean" >
| < CLASS: "class" >
| < ELSE: "else" >
| < EXTENDS: "extends" >
| < FALSE: "false" >
| < IF: "if" >
| < INT: "int" >
| < NEW: "new" >
| < PUBLIC: "public" >
| < RETURN: "return" >
| < STATIC: "static" >
| < THIS: "this" >
| < TRUE: "true" >
| < VOID: "void" >
| < WHILE: "while" >
| < LENGTH: "length">
}

/* JAVA LITERALS */

TOKEN :
{
  < INTEGER_LITERAL: (["0"-"9"])+ >
}

/****************************************************** IDENTIFIERS */

TOKEN :
{
  < IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>)* >
|
  < #LETTER: ["a"-"z", "_","A"-"Z"]>
|
  < #DIGIT: (["0"-"9"])*>
}

/***************************************************** SEPARATORS */

TOKEN :
{
  < LPAREN: "(" >
| < RPAREN: ")" >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < LBRACKET: "[" >
| < RBRACKET: "]" >
| < SEMICOLON: ";" >
| < COMMA: "," >
| < DOT: "." >
}

/***************************************************** OPERATORS */

TOKEN :
{
  < ASSIGN: "=" >
| < LT: "<" >
| < BANG: "!" >
| < SC_AND: "&&" >
| < PLUS: "+" >
| < MINUS: "-" >
| < STAR: "*" >
| < SLASH: "/" >
}

/************************************************************************************************************************
 * Function Start.
 ************************************************************************************************************************/

SimpleNode Start() :{}{
    JavaCompilationUnit() {return jjtThis;}
}

void JavaCompilationUnit() #void :{}
{
  CompilationUnit() <EOF>
}


void CompilationUnit() #void :{}
{
  ( TypeDeclaration() )*
}


void TypeDeclaration() #void :
{}
{
  LOOKAHEAD( ( "public" )* "class" )
  ClassDeclaration()
|
  ";"
}


/*
 * Declaration syntax follows.
 */

void ClassDeclaration() #void :
{}
{
  ( "public" )*
  UnmodifiedClassDeclaration()
}

void UnmodifiedClassDeclaration() :
{Token t0;}
{
  "class" t0=<IDENTIFIER> [ "extends" Name() ]
  ClassBody()
  {jjtThis.value = t0.image;}
}

void ClassBody() #void:
{}
{
  "{" ( ClassBodyDeclaration() )* "}"
}

void NestedClassDeclaration() #void:
{}
{
  ( "static"  | "public"  )*
  UnmodifiedClassDeclaration()
}

void ClassBodyDeclaration() #void:
{}
{
  LOOKAHEAD(2)
  Initializer() #INICIALIZACAO
|
  LOOKAHEAD( ( "static"  | "public"  )* "class" )
  NestedClassDeclaration() # NCD
|
  LOOKAHEAD( [ "public"  ] Name() "(" )
  ConstructorDeclaration() #CONSTRUCTOR
|
  LOOKAHEAD( MethodDeclarationLookahead() )
  MethodDeclaration() #METODO(>1)
|
  FieldDeclaration() #FD
}

// This production is to determine lookahead only.
void MethodDeclarationLookahead() :
{Token t;}
{
  ( "public"  | "static"  )*
  ResultType() t=<IDENTIFIER> "("
  {jjtThis.value = t.image; jjtThis.line = t.beginLine;}
}


void FieldDeclaration() #void:
{}
{
  ( "public" |  "static"  )*
  Type() VariableDeclarator() ( "," VariableDeclarator() )* ";"
}

void VariableDeclarator() #void:
{}
{
  VariableDeclaratorId() [ "="  VariableInitializer() #INIT(>1) ]
}

void VariableDeclaratorId() :
{Token t1;}
{
  t1=<IDENTIFIER> ( "[" "]" {jjtThis.isArray();})*
  {jjtThis.value = t1.image; jjtThis.line = t1.beginLine;}
}

void VariableInitializer() #void:
{}
{
  Expression()
}

void MethodDeclaration() :{}
{
  ( "public"  | "static"   )*
  ResultType() MethodDeclarator()
  ( Block() | ";" )
}

void MethodDeclarator() :
{Token t3;}
{
  t3=<IDENTIFIER> FormalParameters() ( "[" "]" )*
  {jjtThis.value = t3.image; jjtThis.line = t3.beginLine;}
}

void FormalParameters() #void:
{}
{
  "(" [ FormalParameter() ( "," FormalParameter() )* ] ")"
}

void FormalParameter() #void:
{}
{
  Type() VariableDeclaratorId()
}

void ConstructorDeclaration():
{Token t4;}
{
  [ "public"  ]
  t4=<IDENTIFIER> FormalParameters()
  "{"
    [ LOOKAHEAD(ExplicitConstructorInvocation()) ExplicitConstructorInvocation() ]
    ( BlockStatement() )*
  "}"
  {jjtThis.value = t4.image; jjtThis.line = t4.beginLine;}
}

void ExplicitConstructorInvocation() #void:
{}
{
   "this" Arguments() ";"
}

void Initializer() #void:
{}
{
  [ "static" ] Block()
}


/*
 * Type, name and expression syntax follows.
 */

void Type() :
{}
{
  ( PrimitiveType() | Name() ) ( "[" "]" )*
}

void PrimitiveType() :
{Token t;}
{
  t="boolean" {jjtThis.value = t.image; jjtThis.line = t.beginLine;}
|
  t="int" {jjtThis.value = t.image; jjtThis.line = t.beginLine;}

}

void ResultType():
{Token t;}
{
  t="void" {jjtThis.value = t.image; jjtThis.line = t.beginLine;}
|
  Type()
}

void Name() :
{Token t,t1;}
{
  t=<IDENTIFIER>  {jjtThis.value = t.image; jjtThis.line = t.beginLine;}
   ( LOOKAHEAD(2) "." t1=<IDENTIFIER>   {jjtThis.value = t1.image; jjtThis.line = t1.beginLine;})*

}

void NameList() #void:{}
{
  Name() ( "," Name() )*
}


/*
 * Expression syntax follows.
 */

void Expression() #void:
{}
{
  ConditionalAndExpression() [ AssignmentOperator() Expression()  #ASSIGNMENT(2)]
}

void AssignmentOperator() #void:
{}
{
  "="
}

void ConditionalAndExpression() #void:
{}
{
  RelationalExpression() ( "&&"  RelationalExpression() )*
}


void RelationalExpression() #void:
{}
{
  AdditiveExpression() ( LOOKAHEAD(2) ( "<"  ) AdditiveExpression() #LESSTHEN(2) )*
}


void AdditiveExpression() #void:
{}
{
  MultiplicativeExpression() ( ( "+" | "-" )  MultiplicativeExpression() #ADDSUB(2) )*
}

void MultiplicativeExpression() #void:
{}
{
  UnaryExpression() ( ( "*" | "/" )  UnaryExpression() #DIVMULT(2) )*
}

void UnaryExpression() #void:
{}
{
  ( "+" | "-" ) UnaryExpression()
|
  UnaryExpressionNotPlusMinus()
}


void UnaryExpressionNotPlusMinus() #void:
{}
{
  ( "!"  ) UnaryExpressionNotPlusMinus()
|
  LOOKAHEAD( CastLookahead() )
  CastExpression()
|
  PrimaryExpression()
}

void CastLookahead() :
{}
{
  LOOKAHEAD(2)
  "(" PrimitiveType()
|
  LOOKAHEAD("(" Name() "[")
  "(" Name() "[" "]"
|
  "(" Name() ")" ( "!" | "(" | <IDENTIFIER> | "this" | "new" | Literal() )
}


void CastExpression() #void:
{}
{

  "(" Type() ")" UnaryExpressionNotPlusMinus()
}

void PrimaryExpression() #void:
{}
{
  PrimaryPrefix() ( LOOKAHEAD(2) PrimarySuffix() )*
}

void PrimaryPrefix() #void:
{}
{
  Literal()
|
  "this"
|
  "(" Expression() ")"
|
  AllocationExpression()
|
  LOOKAHEAD( ResultType() "." "class" )
  ResultType() "." "class"
|
  Name()
}

void PrimarySuffix():
{Token t10;}
{
  LOOKAHEAD(2)
  "." "this"
|
  LOOKAHEAD(2)
  "." AllocationExpression()
|
   LOOKAHEAD(2)
   "." "length"
|
  "[" Expression() "]"
|
  "." t10=<IDENTIFIER> {jjtThis.value = t10.image; jjtThis.line = t10.beginLine;}
|
  Arguments()

}

void Literal() :
{Token t11;}
{
  t11=<INTEGER_LITERAL> {jjtThis.value = t11.image; jjtThis.line = t11.beginLine;}
|
  BooleanLiteral()

}

void IntegerLiteral() :
{Token t12;}
{
  t12=<INTEGER_LITERAL> {jjtThis.value = t12.image; jjtThis.line = t12.beginLine;}

}



void BooleanLiteral():
{Token t13;}
{
  t13="true" {jjtThis.value = t13.image; jjtThis.line = t13.beginLine;}
|
  t13="false" {jjtThis.value = t13.image; jjtThis.line = t13.beginLine;}

}

void Arguments() #void:
{}
{
  "(" [ ArgumentList() ] ")"
}

void ArgumentList() #void:
{}
{
  Expression() ( "," Expression() )*
}

void AllocationExpression() #void:
{}
{
  LOOKAHEAD(2)
  "new" PrimitiveType() ArrayDimsAndInits()
|
  "new" Name()
    (
      ArrayDimsAndInits()
    |
      Arguments() [ ClassBody() ]
    )
}

void ArrayDimsAndInits() #void:
{}
{
  ( LOOKAHEAD(2) "[" Expression() "]" )+ ( LOOKAHEAD(2) "[" "]" )*
//|
//  ( "[" "]" )+ ArrayInitializer()
}


/*
 * Statement syntax follows.
 */

void Statement() #void :
{}
{
  LOOKAHEAD(2)
  Block()
|
  EmptyStatement()
|
  StatementExpression() ";"
|
  IfStatement()
|
  WhileStatement()
|
  ReturnStatement()
}


void Block() #void:
{}
{
  "{" ( BlockStatement() )* "}"
}

void BlockStatement() #void:
{}
{
  LOOKAHEAD( Type() <IDENTIFIER>)
  LocalVariableDeclaration() ";"
|
  Statement()
|
  UnmodifiedClassDeclaration()

}

void LocalVariableDeclaration() #void:
{}
{
   Type() VariableDeclarator() ( "," VariableDeclarator() )*
}

void EmptyStatement() #void:
{}
{
  ";"
}

void StatementExpression() #void:
{}
{
  PrimaryExpression()
  [
    AssignmentOperator() Expression() #ASSIGNMENT(2)
  ]
}


void IfStatement() #IF :
{}
{
  "if" "(" Expression() #CONDITION ")" Statement() #STATEMENT  [ LOOKAHEAD(1) "else" Statement() #ELSE ]
}

void WhileStatement() #WHILE :
{}
{
    "while" "("
    try {
          Expression() #CONDITION
        }
        catch (ParseException e) {
          System.out.println(e.toString());
          Token t;
          do {
            t = getNextToken();
            } while (t.kind != RPAREN);
        }
    ")" Statement() #BODY
}


void StatementExpressionList() #void:
{}
{
  StatementExpression() ( "," StatementExpression() )*
}


void ReturnStatement() #RETURN :
{}
{
  "return" [ Expression() ] ";"
}